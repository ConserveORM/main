apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.github.conserveorm'
version = '0.8.0'

//downgrade for compatability with Android
sourceCompatibility = '1.7'


sourceSets {
	main {
		java {
			srcDirs = ['src']
		}
	}
	test {
		java {
			srcDirs = ['test']
		}
	}
}

repositories {
    mavenLocal()
	mavenCentral()
	maven {
		//monetdb jars
		url "https://clojars.org/repo/"
	}
}

test {
	filter {
		//don't run tests several times
		includeTestsMatching "com.github.conserveorm.AllTests"
	}
}

dependencies {
	testCompile group: 'junit', name: 'junit', version:'[4,)'
	testCompile group: 'org.apache.derby', name: 'derbyclient', version:'[10,)'
	testCompile group: 'org.apache.derby', name: 'derby', version:'[10,)'
	testCompile group: 'com.h2database', name: 'h2', version:'[1.4,)'
	testCompile group: 'org.postgresql', name: 'postgresql', version:'[9.4,)'
	testCompile group: 'org.hsqldb', name: 'hsqldb', version:'[2.3,)'
	testCompile group: 'mysql', name: 'mysql-connector-java', version:'[6.0,)'
	testCompile group: 'org.xerial', name: 'sqlite-jdbc', version:'[3.15,)'
	testCompile group: 'org.firebirdsql.jdbc', name: 'jaybird', version:'[2.2,)'
	testCompile group: 'monetdb', name: 'monetdb-jdbc', version:'[2.23,10]'
	testCompile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '[1.5,)'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

task javadocJar (type: Jar, dependsOn: javadoc) { 
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {

            from components.java
            
            artifact sourceJar {
                classifier "sources"
            }
            
            artifact javadocJar {
                classifier "javadoc"
            }            
        }
    }
}

artifacts {
    archives javadocJar, sourceJar
}

signing {
    sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'Conserve ORM'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'A simple and powerful Object-Relational Mapping library.'
        url 'https://github.com/ConserveORM/main'

        scm {
          connection 'scm:git:https://github.com/ConserveORM/main.git'
          developerConnection 'scm:git:https://github.com/ConserveORM/main.git'
          url 'https://github.com/ConserveORM/main'
        }

        licenses {
          license {
            name 'GNU Affero General Public License v3'
            url 'https://www.gnu.org/licenses/agpl-3.0.txt'
          }
        }

        developers {
          developer {
            id 'erikjber'
            name 'Erik Berglund'
            email 'erik.berglund@gmail.com'
          }
        }
      }
    }
  }
}
